plugins {
    id 'java'
    id 'application'
    id 'eclipse'
}

sourceCompatibility = JavaVersion.VERSION_12
targetCompatibility = JavaVersion.VERSION_12

repositories {
    jcenter()
}
dependencies {
    def jersey_version = '2.29.1'
    implementation group: 'org.glassfish.jersey', name: 'jersey-bom', version: jersey_version, ext: 'pom'
    implementation group: 'org.glassfish.jersey.containers', name:'jersey-container-grizzly2-http', version: jersey_version
    implementation group: 'org.glassfish.jersey.inject', name: 'jersey-hk2', version: jersey_version

    def jaxb_version = '2.3.1'
    implementation group: 'javax.xml.bind', name: 'jaxb-api', version: jaxb_version

    def gson_version = '2.8.5'
    implementation group: 'com.google.code.gson', name: 'gson', version: gson_version

    def junit_version = '5.5.2'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junit_version
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junit_version
}

application {
    mainClassName = 'pcbe.stock.CLI'
}

compileJava {
    options.compilerArgs += ['--enable-preview']
}

compileTestJava {
    options.compilerArgs += ['--enable-preview']
}

test {
    jvmArgs '--enable-preview'
    useJUnitPlatform()
}

run {
    standardInput = System.in
    jvmArgs '--enable-preview'
}

// run 'gradle eclipseJdt` to enable java 12 preview features in Visual Studio Code 
eclipse.jdt.file.withProperties { props ->
    props['org.eclipse.jdt.core.compiler.problem.enablePreviewFeatures']= 'enabled'
    props['org.eclipse.jdt.core.compiler.problem.reportPreviewFeatures']= 'ignore'
}